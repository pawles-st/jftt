use std::str::FromStr;

use crate::err::ImpError;
use crate::ast::*;

use lalrpop_util::ParseError;

grammar;

extern {
	type Error = ImpError;
}

match {
	r"\s*" => {  },
	r"#[^\n\r]*\r?\n?" => {  },
	_,
}

pub ProgramAll: ProgramAll = {
	Procedures Main => ProgramAll::new(<>),
}

Procedures: Procedures = {
	<mut v: Procedures> <l: @L> "PROCEDURE" <h: ProcHead> "IS" <d: Declarations> <r: @R> "IN" <c: Commands> "END" => {
		v.push(Procedure::new(h, d, c, (l, r)));
		v
	},
	<mut v: Procedures> <l: @L> "PROCEDURE" <h: ProcHead> "IS" <r: @R> "IN" <c: Commands> "END" => {
		v.push(Procedure::new(h, Vec::new(), c, (l, r)));
		v
	},
	=> Vec::new(),
}

Main: Main = {
	<l: @L> "PROGRAM IS" <d: Declarations> <r: @R> "IN" <c: Commands> "END" => Main::new(d, c, (l, r)),
	<l: @L> "PROGRAM IS" "IN" <r: @R> <c: Commands> "END" => Main::new(Vec::new(), c, (l, r)),
}

Commands: Commands = {
	<mut v: Commands> <c: Command> => {
		v.push(c);
		v
	},
	Command => vec![<>],
}

Command: Command = {
	<l: @L> <i: Identifier> ":=" <e: Expression> ";" <r: @R> => Command::Assignment(i, e, (l, r)),
	<l: @L> "IF" <c: Condition> "THEN" <ifc: Commands> "ELSE" <elc: Commands> "ENDIF" <r: @R> => Command::IfElse(c, ifc, elc, (l, r)),
	<l: @L> "IF" <c: Condition> "THEN" <ifc: Commands> "ENDIF" <r: @R> => Command::If(c, ifc, (l, r)),
	<l: @L> "WHILE" <c: Condition> "DO" <wc: Commands> "ENDWHILE" <r: @R> => Command::While(c, wc, (l, r)),
	<l: @L> "REPEAT" <rc: Commands> "UNTIL" <c: Condition> ";" <r: @R> => Command::Repeat(rc, c, (l, r)),
	<l: @L> <p: ProcCall> ";" <r: @R> => Command::ProcedureCall(p, (l, r)),
	<l: @L> "READ" <i: Identifier> ";" <r: @R> => Command::Read(i, (l, r)),
	<l: @L> "WRITE" <v: Value> ";" <r: @R> => Command::Write(v, (l, r)),
}

ProcHead: ProcHead = {
	<Pidentifier> "(" <ArgsDecl> ")" => ProcHead::new(<>),
}

ProcCall: ProcCall = {
	<Pidentifier> "(" <Args> ")" => ProcCall::new(<>),
}

Declarations: Declarations = {
	<mut v: Declarations> "," <p: Pidentifier> => {
		v.push(Declaration::Var(p));
		v
	},
	<mut v: Declarations> "," <p: Pidentifier> "[" <n: Num> "]" => {
		v.push(Declaration::Arr(p, n));
		v
	},
	Pidentifier => vec![Declaration::Var(<>)],
	<Pidentifier> "[" <Num> "]" => vec![Declaration::Arr(<>)],
}

ArgsDecl: ArgumentDeclarations = {
	<mut v: ArgsDecl> "," <p: Pidentifier> => {
		v.push(ArgumentDeclaration::Var(p));
		v
	},
	<mut v: ArgsDecl> "," "T" <p: Pidentifier> => {
		v.push(ArgumentDeclaration::Arr(p));
		v
	},
	Pidentifier => vec![ArgumentDeclaration::Var(<>)],
	"T" <Pidentifier> => vec![ArgumentDeclaration::Arr(<>)],
}

Args: Arguments = {
	<mut v: Args> "," <p: Pidentifier> => {
		v.push(p);
		v
	},
	Pidentifier => vec![<>],
}

Expression: Expression = {
	Value => Expression::Val(<>),
	<Value> "+" <Value> => Expression::Add(<>),
	<Value> "-" <Value> => Expression::Sub(<>),
	<Value> "*" <Value> => Expression::Mul(<>),
	<Value> "/" <Value> => Expression::Div(<>),
	<Value> "%" <Value> => Expression::Mod(<>),
}

Condition: Condition = {
	<Value> "=" <Value> => Condition::Equal(<>),
	<Value> "!=" <Value> => Condition::NotEqual(<>),
	<Value> ">" <Value> => Condition::Greater(<>),
	<Value> "<" <Value> => Condition::Lesser(<>),
	<Value> ">=" <Value> => Condition::GreaterOrEqual(<>),
	<Value> "<=" <Value> => Condition::LesserOrEqual(<>),
}

Value: Value = {
	Num => Value::Number(<>),
	Identifier => Value::Id(<>),
}

Identifier: Identifier = {
	Pidentifier => Identifier::Pid(<>),
	<Pidentifier> "[" <Num> "]" => Identifier::ArrNum(<>),
	<Pidentifier> "[" <Pidentifier> "]" => Identifier::ArrPid(<>),
}

Pidentifier: Pidentifier = {
	r"[_a-z]+" => <>.to_string(),
}

Num: Num = {
	r"[0-9]+" =>? u64::from_str(<>)
		.map_err(|_| ParseError::User{
			error: ImpError::NumberTooBig(<>.to_string())
		}),
}
